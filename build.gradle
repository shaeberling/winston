// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
  repositories {
    jcenter()
  }
}

// To create a gradle wrapper.
task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}


// Code Coverage and reporting.
// The coveralls gradle plugin cannot handle multiple projects, so we have to flatten
// the reports here in order to get coverage for all subprojects.
def publishedProjects = subprojects.findAll {
  (it.path == ':winston-libs') || (it.path == ':winston-server')
}
evaluationDependsOn(':winston-libs')
evaluationDependsOn(':winston-server')


apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'


task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
   dependsOn = publishedProjects.jacocoTestReport
   sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
   classDirectories = files(publishedProjects.sourceSets.main.output)
   executionData = files(publishedProjects.jacocoTestReport.executionData)
   reports {
       xml.enabled true
       xml.destination ="${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
   }
   setOnlyIf { true }
}

coveralls {
    sourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs).files.absolutePath
}

tasks.coveralls {
    dependsOn jacocoRootReport
}

